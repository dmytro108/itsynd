### Solution
This solution extends the original task by allowing you to create any number of VPCs and create peering connections between all of these **VPC**s in a each-to-each manner.

#### VPCs
The [vpc module](https://registry.terraform.io/modules/terraform-aws-modules/vpc/aws/latest) is configured with `count` parametr to create the given number of VPCs resulting in an array of created VPCs.

Along with a number of VPCs to create a base CIDR must be specified. The configuration generate all the CIDRs for private and public subnets for each VPC based on the single given CIDR.
For instance, if a base CIDR 10.0.0.0/16 was specified and two VPCs are to create, it will create:
- VPC[0]:
  - CIDR: 10.0.0.0/16
  - private subnet: 10.0.0.0/24
  - public subnet: 10.0.100.0/24
- VPC[1]:
  - CIDR: 10.1.0.0/16
  - private subnet: 10.1.0.0/24
  - public subnet: 10.1.100.0/24

The number of private and public subnets is also a variable and can be configured. 

The array of VPCs is generated in the `locals` section of the configuration.
```hcl
locals {
  # map of VPC CIDR blocks with private and public subnets CIDRs:
  # {vpc_cidr: 10.x.0.0/16
  # private_subnet_cidrs: 10.x.y.0/24
  # public_subnet_cidrs: 10.x.10y.0/24}
  cidrs = [for vcidr in [for x in range(var.vpc_num) : cidrsubnet(var.vpc_cidr_prefix, 4, x)] : {
    vpc_cidr             = vcidr
    private_subnet_cidrs = [for y in range(var.private_subnet_num) : cidrsubnet(vcidr, 8, y)]
    public_subnet_cidrs  = [for y in range(var.public_subnet_num) : cidrsubnet(vcidr, 8, 100 + y)]
  }]
}
```
All the VPCs creating code is in the file [vpc.tf](vpc.tf)

#### Peering connections
Peering connections between all **VPC**s are configured using the `for_each` meta argument. 

A special string array is used as a parameter for the meta-argument `for_each`. Each element of the string array  contains a pair of the VPC array indexes, created by the vpc module. The sting array contains all possible peering pairs of the created **VPC**s - `["0,1", "0,2", "0,3",...,"0,n", ..., "n-1, n"]`. The array is created in the `locals` section. 

For each **VPC** pair, a resource `aws_vpc_peering_connection` is created where the indexes from each element of the pairs array are used to obtain the **VPC** IDs generated by the *vpc module*.
Routes for the routing tables of each of the subnets in each **VPC** are generated in a similar way. The string *("x,y")* containing the **VPC** index pair is also a key in the peering connection resource set. The CIDR of one of the **VPC**s of the peering pair is used as the destination of each route, and the peering connection itself is used as the gateway. This route is added to the routing table associated with a subnet belonging to another VPC from the same peering pair.
The array of **VPC** pairs generated as following:
```hcl
locals {
  # List of inxes of VPCs which joint into peering pairs: ["0,1",...,"0,n",..., "n-1,n"]
  # The list contains pairs by the scheme "each to each"
  peering_pairs = flatten([for vpc1 in range(var.vpc_num) : [
    for vpc2 in range(vpc1 + 1, var.vpc_num, 1) :
    join(",", [vpc1, vpc2])
    ]
  ])
}
```

All peering connections are configured as following:
```hcl
resource "aws_vpc_peering_connection" "peering" {
  for_each    = toset(local.peering_pairs)
  peer_vpc_id = module.vpc[tonumber(split(",", each.value)[0])].vpc_id
  vpc_id      = module.vpc[tonumber(split(",", each.value)[1])].vpc_id
  auto_accept = true
  tags = {
    Name = "peer-${each.value}"
  }
}
```

A route is configured as following:
```hcl
resource "aws_route" "direct_privat" {
  for_each = toset(keys(aws_vpc_peering_connection.peering))

  route_table_id            = module.vpc[tonumber(split(",", each.value)[1])].private_route_table_ids[0]
  destination_cidr_block    = module.vpc[tonumber(split(",", each.value)[0])].vpc_cidr_block
  vpc_peering_connection_id = aws_vpc_peering_connection.peering[each.value].id
}
```
All the VPCs peering code is in the file [peering.tf](peering.tf)
#### Tools
The code was checked with **tflint**, **tfsec** and **checkov**. The documentation was created with **terraform-docs**
